plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.monew'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    jacoco {
        toolVersion = "0.8.10"
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport // 테스트 후 커버리지 자동 생성
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            csv.required = false
            html.required = true
            html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        implementation 'io.micrometer:micrometer-core'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }
}